

apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

// =================================================================================================
// This block must not be modified unless BumpVersion.groovy task updates accordingly.
    def android_version_code=2
    def android_version_name="2.0"

    def getAppVersionCode = { ->
        def code = project.hasProperty('versionCode') ? versionCode.toInteger() : android_version_code
        return code
    }

    def getAppVersionName = { ->
        def name = project.hasProperty('versionName') ? versionName : android_version_name
        return name
    }
//==================================================================================================


    defaultConfig {
        applicationId "com.sample.bitrise.bitrise"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode getAppVersionCode()
        versionName getAppVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    productFlavors {

        playStore {

        }
        live {
            applicationIdSuffix "live"
        }
        stage {
            applicationIdSuffix "stage"

        }
        dev {
            applicationIdSuffix "dev"
        }
        qa {
            applicationIdSuffix "qa"
        }
    }


    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    // beta release 배포에는 영향을 주지 않고 Build -> generate signed apk 에서만 파일 이름이 적용 되도록!
                    def formattedDate = new Date().format('yyyyMMdd_HHmm')
                    def fullName = output.outputFile.name
                    output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(fullName,
                            "yanolja_v${defaultConfig.versionName}_c${defaultConfig.versionCode}_${variant.baseName}_${formattedDate}.apk"))
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
}

